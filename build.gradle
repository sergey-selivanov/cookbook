/*

    To make release and increase version:
        gradle -Preckon.stage=final -Preckon.scope=patch reckonTagPush
        gradle -Preckon.stage=final -Preckon.scope=minor reckonTagPush
        gradle -Preckon.stage=final -Preckon.scope=major reckonTagPush

*/

plugins {
    id 'application'
    id 'eclipse'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.14.0'

//    id 'net.saliman.properties' version '1.5.1'
    id 'com.github.ben-manes.versions' version '0.22.0'
    id 'org.ajoberstar.reckon' version '0.11.0'
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.h2database:h2:1.4.199'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.1'
    compile 'org.apache.logging.log4j:log4j-core:2.12.1'
}


if(!project.hasProperty('reckon.scope')){
    project.ext.'reckon.scope' = 'patch'    // override default 'minor'
}

reckon {
    scopeFromProp()
    stageFromProp('dev', 'final')
}


javafx {
    version = "12.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web' ]
}

mainClassName = "$moduleName/org.sergeys.cookbook.ui.CookBook"

eclipse {
    classpath {
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') }.each {
                    it.entryAttributes['module'] = 'true'
                }
            }
        }
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'hellofx'
    }
}
