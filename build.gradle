/*

    To make release and increase version:
        gradle -Preckon.stage=final -Preckon.scope=patch reckonTagPush
        gradle -Preckon.stage=final -Preckon.scope=minor reckonTagPush
        gradle -Preckon.stage=final -Preckon.scope=major reckonTagPush

*/

plugins {
    id 'application'
    id 'eclipse'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.17.3'

//    id 'net.saliman.properties' version '1.5.1'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.ajoberstar.reckon' version '0.12.0'
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.h2database:h2:1.4.200'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.1' // forcemerge
    //compile 'org.apache.logging.log4j:log4j-core:2.12.1'

    // As of version 2.10.0 the Log4j API is a Java module
    // errors in jlink
    //compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'


    // TODO deprecated methods in HtmlImporter, refactor
//    compile(group: 'xerces', name: 'xercesImpl', version: '2.12.0')
//    {
//        exclude module: 'xml-apis'
//    }
    compile fileTree(dir: 'lib', include: ['*.jar'])
}


if(!project.hasProperty('reckon.scope')){
    project.ext.'reckon.scope' = 'patch'    // override default 'minor'
}

reckon {
    scopeFromProp()
    stageFromProp('dev', 'final')
}


javafx {
    //version = '13.0.1'
    version = '14'
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web' ]
}

mainClassName = "$moduleName/org.sergeys.cookbook.ui.CookBook"

eclipse {
    classpath {
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') }.each {
                    it.entryAttributes['module'] = 'true'
                }
            }
        }
    }
}


// https://github.com/beryx/badass-jlink-plugin/issues/72
// Execute: ./gradlew suggestMergedModuleInfo
// Remove the lines contaning provides clauses and put the resulting mergedModule configuration in your jlink block.

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'] // --bind-services ?
    launcher {
        name = 'cookbook'
    }
    forceMerge('log4j-api') // https://github.com/beryx/badass-jlink-plugin/issues/14

    mergedModule {
        additive = true
        //requires 'java.desktop'
        //requires transitive 'java.sql'
        //uses 'java.sql.Driver'
        //provides 'java.sql.Driver' with 'org.hsqldb.jdbc.JDBCDriver'
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
